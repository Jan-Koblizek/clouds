/*
Copyright (c) 2020 Jan Koblížek

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

#pragma kernel CSMain

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture
RWTexture3D<float4> Result;
float Time;

float hash12(float2 p) {
	p = 50.0*frac(p*0.3183099);
	return frac(p.x*p.y*(p.x + p.y));
}

float hash13(float3 p3) {
	p3 = frac(p3 * 1031.1031);
	p3 += dot(p3, p3.yzx + 19.19);
	return frac((p3.x + p3.y) * p3.z);
}

float3 hash33(float3 p3) {
	p3 = frac(73 * p3 * float3(.1731, 1.1030, 13.0973));
	p3 += dot(p3, p3.yxz + 19.19);
	return frac((p3.xxy + p3.yxx)*p3.zyx);
}

float valueHash(float3 p3) {
	p3 = frac(p3 * 0.1031);
	p3 += dot(p3, p3.yzx + 19.19);
	return frac((p3.x + p3.y) * p3.z);
}


// Noise functions used for cloud shapes
// 3D perlin noise
float perlin(in float3 x, float tile) {
	float3 p = floor(x);
	float3 f = frac(x);
	f = f * f*(3.0 - 2.0*f);

	return lerp(lerp(lerp(valueHash(fmod(p + float3(0, 0, 0), tile)),
		valueHash(fmod(p + float3(1, 0, 0), tile)), f.x),
		lerp(valueHash(fmod(p + float3(0, 1, 0), tile)),
			valueHash(fmod(p + float3(1, 1, 0), tile)), f.x), f.y),
		lerp(lerp(valueHash(fmod(p + float3(0, 0, 1), tile)),
			valueHash(fmod(p + float3(1, 0, 1), tile)), f.x),
			lerp(valueHash(fmod(p + float3(0, 1, 1), tile)),
				valueHash(fmod(p + float3(1, 1, 1), tile)), f.x), f.y), f.z);
}

// 3D Voronoi/Worley noise
float voronoi(float3 x, int tiles) {
	float3 fragPos = x;
	int3 p = floor(fragPos);
	float minDist = 10.;
	for (int i = -1; i < 2; i++) {
		for (int j = -1; j < 2; j++) {
			for (int k = -1; k < 2; k++) {
				int n = ((tiles + i + p.x) % tiles) + ((tiles + j + p.y) % tiles) * tiles + ((tiles + k + p.z) % tiles) * tiles * tiles;
				float n2 = float(n) / tiles;

				float3 pos1 = hash33(float3(n2, n2, n2));
				float3 pos2 = (0.5 * sin(pos1 * Time * 100) + 0.5) + float3(i + p.x, j + p.y, k + p.z);

				float d = length(pos2 - fragPos);
				if (d < minDist) {
					minDist = d;
				}
			}
		}
	}

	return 1. - minDist;
	//return float3(p) / tiles;
}

//Tilable voronoi - allows seamless tiling of the texture
float tilableVoronoi(float3 x, const int octaves, float tile) {
	float f = 1.;
	float a = 1.;
	float c = 0.;
	float w = 0.;

	if (tile > 0.) f = tile;

	for (int i = 0; i < octaves; i++) {
		c += a * voronoi(x * f, f);
		f *= 2.0;
		w += a;
		a *= 0.5;
	}

	return c / w;
}

//Tilable Perlin - allows seamless tiling of the texture
float tilablePerlin(float3 p, const int octaves, float tile) {
	float f = 1.;
	float a = 1.;
	float c = 0.;
	float w = 0.;

	if (tile > 0.) f = tile;

	for (int i = 0; i < octaves; i++) {
		c += a * perlin(p * f, f);
		f *= 2.0;
		w += a;
		a *= 0.5;
	}

	return c / w;
}

[numthreads(8,8,8)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
	float3 coord = float3(id) / 31;

	//Returns 3D textures of different siyes of the 3D Voronoi noise in the texture color channels
	float r = tilableVoronoi(coord, 5, 3.);
	float g = tilableVoronoi(coord, 4, 5.);
	float b = tilableVoronoi(coord, 4, 11.);

    Result[id.xyz] = float4(r, g, b, 1.0);
}
